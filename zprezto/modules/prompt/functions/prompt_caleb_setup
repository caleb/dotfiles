#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Caleb Land <caleb@land.fm>
#
# Screenshots:
#   http://i.imgur.com/nBEEZ.png
#

# Load dependencies.
pmodload 'helper'

function prompt_caleb_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_caleb_pwd="$MATCH"
    unset MATCH
  else
    _prompt_caleb_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_caleb_repo_type {
  git branch >/dev/null 2>/dev/null && _prompt_caleb_repo_type='±' && return
  hg root >/dev/null 2>/dev/null && _prompt_caleb_repo_type='☿' && return

  _prompt_caleb_repo_type='○'
}

function prompt_caleb_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_caleb_pwd

  # Repo Type
  prompt_caleb_repo_type

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby version information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_caleb_git_status_wrap {
  local var=$1
  var="${var#"${var%%[![:space:]]*}"}"   # remove leading whitespace characters
  var="${var%"${var##*[![:space:]]}"}"   # remove trailing whitespace characters

  echo -n "$var"
}

function prompt_caleb_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info & ruby-info before each command.
  add-zsh-hook precmd prompt_caleb_precmd

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
  zstyle ':prezto:module:git:info:dirty' format '%%B%F{red}'
  zstyle ':prezto:module:git:info:clean' format '%B%F{green}'
  zstyle ':prezto:module:git:info:action' format '%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '%%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{yellow}↑%f%%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{yellow}↓%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format '%%B%F{cyan}★%f%%b'
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format '%%B%F{magenta}→%f%%b'
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{yellow}★%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{yellow}⌥%f%%b'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt-branch' ' %D%C$(coalesce "%b" "%p" "%c")%s%f%%b' \
    'prompt-status' ' $(prompt_caleb_git_status_wrap "%A%B%S%a%d%m%r%U%u")'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format ' %F{magenta}%v%f'

  # Define prompts.
  PROMPT='
%F{cyan}${_prompt_caleb_pwd}${git_info:+${(e)git_info[prompt-branch]}}${git_info:+${(e)git_info[prompt-status]}}${ruby_info:+${(e)ruby_info[version]}}%f%b
${_prompt_caleb_repo_type} %# '

  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_caleb_setup "$@"

